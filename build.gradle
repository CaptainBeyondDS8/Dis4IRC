plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.21'
    id 'net.minecrell.licenser' version "0.4.1"
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
}

group 'io.zachbr'
version '1.2.1-SNAPSHOT'

repositories {
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap/' }
    maven { url 'https://dl.bintray.com/dv8fromtheworld/maven/' }
    maven { url 'https://repo.spongepowered.org/maven' }
    mavenCentral()
    jcenter()
}

jar {
    manifest.attributes(
            'Main-Class': "io.zachbr.dis4irc.Dis4IRCKt",
    )
}

jar.enabled = false
build.dependsOn(shadowJar)

import org.apache.tools.ant.filters.ReplaceTokens

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }

    return stdout.toString().trim()
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [
                'project-name'       : rootProject.name,
                'project-version'    : version,
                'project-git-hash'   : getGitHash(),
                'project-source-repo': 'https://github.com/zachbr/Dis4IRC'
        ]
    }
}

artifacts {
    shadowJar
}

shadowJar {
    classifier = null
    preserveFileTimestamps = false
    reproducibleFileOrder = true

    from "LICENSE.md"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.kitteh.irc:client-lib:7.3.0"
    implementation("net.dv8tion:JDA:4.2.0_222") {
        exclude module: 'opus-java'
    }
    implementation "club.minnced:discord-webhooks:0.5.0"

    implementation 'org.json:json:20180813'
    implementation "org.spongepowered:configurate-hocon:3.7.1"
    implementation "com.atlassian.commonmark:commonmark:0.15.2"
    implementation "com.atlassian.commonmark:commonmark-ext-gfm-strikethrough:0.15.2"

    implementation "org.slf4j:slf4j-api:1.7.30"
    implementation "org.apache.logging.log4j:log4j-core:2.13.2"
    runtime "org.apache.logging.log4j:log4j-slf4j-impl:2.13.2"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:1.4.21"
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

dependencyManagement {
    imports {
        mavenBom 'org.apache.logging.log4j:log4j-bom:2.13.2'
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

// updateLicenses | checkLicenses
license {
    header = project.file('HEADER.txt')
    ext {
        name = rootProject.name
        year = "2018-2021"
    }
}

test {
    testLogging.showStandardStreams = true
    useJUnitPlatform()
}

